PageRank~\cite{Page:2001:MNR} is a well known graph algorithm that is used to
compute the relative relevance of web pages. The standard formulation of the
PageRank algorithm uses three $n \times n$ matrices (where $n$ is the number of
pages):

\begin{itemize}

   \item An adjacency matrix $A$, where $A_{ij}$ is $1$ if page $i$ has an
      outgoing link to $j$;

   \item A transition matrix $P$, where $P_{ij} = A_{ij}/deg(i)$ and $\deg(i)$
      is the number of outgoing links for page $i$;

   \item A \scare{Google matrix} $G$, where $G = \alpha P + (1-\alpha)I$, where
      $\alpha$ is the \emph{damping factor} and $I$ is a $n \times n$ matrix
      where $I_{ij}$ is $1$. The damping factor is the probablity of a user
      jumping to a random page instead of following the links of the current
      page.

\end{itemize}

The PageRank vector $x$ of size $1 \times n$ is the solution to the following
linear system:

\begin{align}
x = G x
\end{align}

To solve this problem, it is possible to use an iterative method by starting
with an initial vector $x(0)$ where all pages start with the same value (that
adds up to 1) and then perform the following computation:

\begin{align}
x(t + 1) = G x(t)
\end{align}

In order to distribute this calculation, we have the following formula for page
$i$:

\begin{align}
x_{i}(t + 1) = G_{i} x(t)\label{eq:language:pagerank}
\end{align}

Where $G_{i}$ is the row of the $G$ matrix that corresponds to the inbound links
of page $i$. Note that we also do not need to have the complete $x(t)$ vector as
most elements of $G_{i}$ are $0$.

The LM code for this iterative computation is shown in
Fig.~\ref{language:code:pagerank} and the code starts by declaring six
predicates, namely: \code{outbound}, that specifies outbound links where the
third argument represents a value in the transition matrix $P$;
\code{numInbound}, with the number of inbound links; \code{pagerank}, to
represent the PageRank of a page; \code{accumulator}, to accumulate the incoming
neighbor's PageRank values; \code{neighbor-pagerank}, to represent an incoming
PageRank value; and \code{start} to initialize a node.  Since this program is
synchronous, the PageRank values of all nodes must be computed before the next
PageRank is computed.

We use constant definitions provided by LM
(lines~\ref{line:language:spagerank_const1}-\ref{line:language:spagerank_const2})
to refer to constant values used throughout the program. The \code{damping}
constant is the damping factor $\alpha$ used in the PageRank calculations. The
constant \code{iterations} reads the number of iterations to execute from the
program's input arguments and \code{pages} is assigned to \code{@world}, a
special constant that evaluates to the number of nodes in the program (in this
case, the number of pages).

The initial PageRank representing $x_i(0)$ is initialized in the first rule
(lines
\ref{line:language:spagerank_first1}-\ref{line:language:spagerank_first2}) along
with the accumulator. All the initial PageRank values form the initial $x(0)$
vector. The second rule of the program
(lines~\ref{line:language:spagerank_second1}-\ref{line:language:spagerank_second2})
propagates a newly computed PageRank value to all neighbors and represents a
step in the iterative method for a column in the $G$ matrix. The fact
\code{neighbor-pagerank} informs the neighbor node about the PageRank value of
node \code{A} for iteration \code{Iter + 1}. For every iteration, each node will
accumulate all the \code{neighbor-pagerank} facts into the
\code{accumulator} fact (lines
\ref{line:language:spagerank_fourth1}-\ref{line:language:spagerank_fourth2}).
When all inbound neighbor PageRank values are accumulated, the third rule
(lines~\ref{line:language:spagerank_third1}-\ref{line:language:spagerank_third2})
is derived and a PageRank value is generated for iteration \code{Iter}.

\begin{figure}[h!]
\begin{Verbatim}[numbers=left,fontsize=\codesize,commandchars=\\\[\]]
type outbound(node, node, float).\hfill// Predicate declaration
type numInbound(node, int).
type linear pagerank(node, float Rank, int Iteration).
type linear accumulator(node, float Acc, int Left, int Iteration).
type linear neighbor-pagerank(node, node Neighbor, float Rank, int Iteration).
type linear start(node).

const damping = 0.85.\hfill// probability of user following a link in the current page\label[line:language:spagerank_const1]
const iterations = str2int(@arg1).\hfill// iterations to compute
const pages = @world.\hfill// number of pages in the graph.\label[line:language:spagerank_const2]

start(A), !numInbound(A, T)\label[line:language:spagerank_first1]\hfill// Rule 1: initialize pagerank and accumulator
   -o accumulator(A, 0.0, T, 1), pagerank(A, 1.0 / float(pages), 0).\label[line:language:spagerank_first2]

pagerank(A, V, Iter), \label[line:language:spagerank_second1]\hfill// Rule 2: propagate pagerank
Iter < iterations
   -o {B, W | !outbound(A, B, W) -o neighbor-pagerank(B, A, V * W, Iter + 1)}.\label[line:language:spagerank_second2]

neighbor-pagerank(A, B, V, Iter),\label[line:language:spagerank_fourth1]\hfill// Rule 3: accumulate neighbor's value
accumulator(A, Acc, T, Iter)
   -o accumulator(A, Acc + V, T - 1, Iter).\label[line:language:spagerank_fourth2]

accumulator(A, Acc, 0, Iter),\label[line:language:spagerank_third1]\hfill// Rule 4: generate new pagerank
!numInbound(A, T),
V = damping + (1.0 - damping) * Acc,
Iter <= iterations
   -o pagerank(A, V, Iter), accumulator(A, 0.0, T, Iter + 1).\label[line:language:spagerank_third2]
	
start(A).\hfill// Initial facts
\end{Verbatim}
\caption{Synchronous PageRank program.}
\label{language:code:pagerank}
\end{figure}

The synchronous version of the PageRank algorithm has a fairly high level of
concurrency. First, the program starts on all nodes of the graph, which makes
the program trivial to parallelize. However, because this is a synchronous
algorithm, there is a data dependency between PageRank iterations, i.e., nodes
can only compute their next iteration after they receive all the neighbor's
PageRank values from the previous iteration.

Appendix~\ref{section:appendix:pagerank} further expands this section with the
asynchronous version of PageRank and also includes a proof of correctness.
